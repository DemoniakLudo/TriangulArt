START	EQU #6000
LENGTH	EQU 16244

	ORG #4000

	DI
	LD HL,START
	LD DE,LENGTH
	LD	BC,#7F10
	OUT	(C),C      
	LD	IX,TabColor 
	PUSH HL
	PUSH DE
	PUSH DE
	LD	BC,#F610
	OUT	(C),C
	EXX 
	POP DE
	INC DE        ;EXTRA BYTE FOR CHECKSUM
	EXX 
	LD B,#F5
	IN A,(C)
	LD C,A
LSHT:
	LD E,#20
LCNT:
	LD D,91
	LD A,9
	CALL LEDG2
	JR NC,LSHT
	LD A,145
	CP D
	JR NC,LSHT
	DEC E
	JR NZ,LCNT
LSYNC:
	LD D,91
	CALL LEDG1
	JR NC,LSHT
	LD A,112    ;91+21
	CP D
	JR C,LSYNC
	CALL LEDG1
	JR NC,LSHT
	LD DE,#B801
L8BIT:
	LD A,9
L8BENT:
	CALL LEDG2
	JR NC,ERR
	LD A,#D4
	CP D
	RL E
	LD D,#B8
	JR NC,L8BIT
	LD (HL),E
	INC HL
	LD E,1
	EXX 
	DEC DE
	LD A,D
	OR E
	EXX 
	LD A,6
	JR NZ,L8BENT
ERR:
	LD	BC,#F600
	OUT	(C),C
	POP DE
	POP HL
	XOR A
CSL2:
	ADD (HL)
	INC HL
	DEC DE
	LD C,A
	LD A,D
	OR E
	LD A,C
	JR NZ,CSL2
	SUB (HL)
	CP 1          ;CARRY SET IF CHECKSUM=0
LDEND:
	JP C,#9F0B        ;RETURN IF NO ERRORS
ERROR:
	RET

LEDG2:
	CALL LEDGE
	RET NC
LEDG1:
	LD A,11
LEDGE:
	DEC A
	JR NZ,LEDGE
LSAMP:
	INC D
	RET Z
	LD B,#F5
	IN A,(C)
	XOR C
	JP P,LSAMP
	LD B,#7F
	LD A,C
	CPL 
	LD C,A
	RRA 
	JR	C,TestVal
	INC	IX
	TestVal
	LD	A,(IX+0)
	AND	A
	JR	NZ,OutOk
	LD	A,#55
	LD	IX,TabColor
OutOk
	OUT (C),A
	SCF 
	RET 

TabColor:
	DB	#55,#4B,#4C
