	ORG #A000

STADDR	EQU #6000
LENGTH	EQU 16244

	LD HL,STADDR
	LD DE,LENGTH

	LD A,#10
	CALL MOTOR
	DI 
	PUSH HL
	PUSH DE
	XOR A
CSL2:
	ADD A,(HL)
	INC HL
	DEC DE
	LD C,A
	LD A,D
	OR E
	LD A,C
	JR NZ,CSL2
	LD (HL),A
	POP DE
	INC DE
	LD HL,#0C00
	LD A,#30
	LD B,A
SLEAD:
	DJNZ SLEAD
	LD B,#F6
	OUT (C),A
	XOR #20
	LD B,#A4
	DEC L
	JR NZ,SLEAD
	DEC B
	DEC H
	JP P,SLEAD
	LD B,#2F+5
SYNC1:
	DJNZ SYNC1
	LD B,#F6
	OUT (C),A
	LD A,#10
	LD B,#37
SYNC2:
	DJNZ SYNC2
	LD B,#F6
	OUT (C),A
	POP HL
	LD C,(HL)
	NOP           ;NOPS ARE ESSENTIAL FOR TIMING!!
	NOP 
	LD B,#23
	SCF 
	NOP 
	JR BITS8
SBIT2:
	LD A,#10
	BIT 7,A
SBIT1:
	DJNZ SBIT1
	JR NC,SOUT
	LD B,#2B
SSET:
	DJNZ SSET
SOUT:
	LD B,#F6
	OUT (C),A
	LD B,#26
	JR NZ,SBIT2
	DEC B
	AND A
BITS8:
	RL C
	LD A,#30
	JR NZ,SBIT1
	DEC DE
	INC HL
	LD B,#20
	LD C,(HL)
	LD A,E
	OR D
	SCF 
	JR NZ,BITS8
	LD BC,0
WAITOFF:
	DEC BC
	LD A,B
	OR C
	JR NZ,WAITOFF
MOTOR:
	LD B,#F6
	OUT (C),A
	EI
	RET 

